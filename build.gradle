plugins {
    id 'java'
    id 'signing'
    id 'maven-publish'
    id 'jacoco'
    id 'pl.allegro.tech.build.axion-release' version '1.10.1'
    id 'com.github.kt3k.coveralls' version '2.8.2'
}

scmVersion {
    tag {
        prefix = 'RELEASE'
    }
}

group 'uk.co.caeldev'
project.version = scmVersion.version

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    implementation "ch.qos.logback:logback-classic:1.2.3"
    implementation "com.google.guava:guava:27.1-jre"
    implementation "com.datastax.cassandra:cassandra-driver-mapping:3.7.1"
    implementation "com.datastax.cassandra:cassandra-driver-core:3.7.1"
    implementation "com.squareup:javapoet:1.11.1"
    implementation "com.google.auto.service:auto-service:1.0-rc5"
    implementation "org.apache.commons:commons-text:1.6"


    testImplementation "org.junit.jupiter:junit-jupiter-api:5.4.1"
    testImplementation "org.junit.jupiter:junit-jupiter-params:5.4.1"
    testImplementation "org.assertj:assertj-core:3.12.2"
    testImplementation "org.awaitility:awaitility:3.1.6"
    testImplementation "uk.org.fyodor:fyodor-core:1.0.0"
    testImplementation "org.mockito:mockito-junit-jupiter:2.27.0"
    testImplementation "org.cassandraunit:cassandra-unit:3.11.2.0"
    testImplementation "com.google.testing.compile:compile-testing:0.17"
    testImplementation files(org.gradle.internal.jvm.Jvm.current().getToolsJar())

    testRuntime "org.junit.jupiter:junit-jupiter-engine:5.4.1"
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier = 'javadoc'
}

jar {
    archivesBaseName = rootProject.name
    group = group
    version = version
    manifest {
        attributes 'Implementation-Title': 'cassitory',
                'Implementation-Version': version,
                'Built-By': System.getProperty('user.name'),
                'Built-Date': new Date(),
                'Built-JDK': System.getProperty('java.version'),
                'Built-Gradle': gradle.gradleVersion
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled false
        html.enabled false
    }
}

coveralls {
    jacocoReportPath 'build/reports/jacoco/test/jacocoTestReport.xml'
}

tasks.coveralls {
    dependsOn 'check'
}

test {
    useJUnitPlatform()

    testLogging {
        events 'PASSED', 'FAILED', 'SKIPPED'
    }

    jacoco {
        destinationFile = file("$buildDir/jacoco/jacocoTest.exec")
        classDumpDir = file("$buildDir/jacoco/classpathdumps")
    }
}

def ossUsername = hasProperty('ossrhUsername') ? ossrhUsername : System.getenv('ossrhUsername')
def ossPassword = hasProperty('ossrhPassword') ? ossrhPassword : System.getenv('ossrhPassword')

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifactId = rootProject.name
            groupId = group
            version = version

            artifact sourcesJar
            artifact javadocJar

            pom {
                name = "cassitory"
                description = "Library to manage redundancy tables in Cassandra"
                url = "https://github.com/caelcs/cassitory"

                scm {
                    url = "https://github.com/caelcs/cassitory.git"
                    connection = "https://github.com/caelcs/cassitory.git"
                    developerConnection = "https://github.com/caelcs/cassitory.git"
                }

                licenses {
                    license {
                        name = "MIT"
                        url = "http://opensource.org/licenses/MIT"
                        distribution = "repo"
                    }
                }

                developers {
                    developer {
                        id = "caelcs"
                        name = "Adolfo E Custidiano S"
                        email = "adolfoecs@gmail.com"
                    }
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = "${ossUsername}"
                password = "${ossPassword}"
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
    required = { gradle.taskGraph.hasTask("uploadArchives") && !version.endsWith("SNAPSHOT") }
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
    gradleVersion = '5.4.1'
}
